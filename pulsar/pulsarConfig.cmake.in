# Modules are created without a prefix
set(CMAKE_SHARED_MODULE_PREFIX "")

# Find the dependencies for pulsar
# Add the install path for pulsar to the search path
set(PULSAR_INSTALL_ROOT ${CMAKE_CURRENT_LIST_DIR})
get_filename_component(PULSAR_INSTALL_ROOT ${PULSAR_INSTALL_ROOT} DIRECTORY)
get_filename_component(PULSAR_INSTALL_ROOT ${PULSAR_INSTALL_ROOT} DIRECTORY)
get_filename_component(PULSAR_INSTALL_ROOT ${PULSAR_INSTALL_ROOT} DIRECTORY)

list(APPEND CMAKE_PREFIX_PATH "${PULSAR_INSTALL_ROOT}")

# And the find macros
list(APPEND CMAKE_MODULE_PATH "${PULSAR_INSTALL_ROOT}/cmake")


# Find external projects
find_package(memwatch REQUIRED)
find_package(pybind11 REQUIRED)
find_package(bphash REQUIRED)
find_package(bpprint REQUIRED)
find_package(libtaskforce REQUIRED)


# Create the target to link against
add_library(pulsar INTERFACE)

#######################################
# Add the various include directories
#######################################
# Main pulsar headers
target_include_directories(pulsar INTERFACE ${CMAKE_CURRENT_LIST_DIR}/include)

# Main dependencies (mark as system includes)
list(APPEND PULSAR_CXX_INCLUDES @EIGEN3_INCLUDE_DIR@)
list(APPEND PULSAR_CXX_INCLUDES @MPI_CXX_INCLUDE_PATH@)
list(APPEND PULSAR_CXX_INCLUDES @PULSAR_CORE_PYTHON_INCLUDE_DIRS@)
list(APPEND PULSAR_CXX_INCLUDES ${pybind11_INCLUDE_DIR})
list(APPEND PULSAR_CXX_INCLUDES ${MEMWATCH_INCLUDE_DIR})
target_include_directories(pulsar SYSTEM INTERFACE ${PULSAR_CXX_INCLUDES})


#######################################
# Add interface libraries to link to
#######################################
target_link_libraries(pulsar INTERFACE bphash
                                       bpprint
                                       libtaskforce 
)

#######################################
# Other helpful variables
#######################################
set(PULSAR_TESTING_PATH ${PULSAR_INSTALL_ROOT}/modules/pulsar/testing)
set(PULSAR_RUNTEST ${PULSAR_TESTING_PATH}/RunTest.py)

#######################################
# Include the macros for testing
# (should be in this directory)
#######################################
include(${CMAKE_CURRENT_LIST_DIR}/CTestMacros.cmake)
